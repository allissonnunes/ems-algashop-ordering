buildscript {
    dependencies {
        classpath 'org.flywaydb:flyway-database-postgresql:11.14.1'
    }
}

plugins {
    id 'idea'
    id 'java'
    id 'org.springframework.boot' version '4.0.3'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.springframework.cloud.contract' version '5.0.0'
    id 'org.flywaydb.flyway' version '12.0.0'
}

group = 'br.dev.allissonnunes.algashop'
version = '0.0.1-SNAPSHOT'
description = 'Ordering Microservice'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    mockitoAgent
}

ext {
    set('mapstructVersion', "1.6.3")
    set('mapstructSpringExtensionsVersion', "2.0.0")
    set('lombokMapstructBindingVersion', "0.2.0")
    set('springCloudVersion', "2025.1.0")
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-flyway'
    implementation 'org.springframework.boot:spring-boot-starter-restclient'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-webmvc'
    implementation 'com.fasterxml.uuid:java-uuid-generator:5.1.1'
    implementation 'commons-validator:commons-validator:1.10.0'
    implementation 'com.github.gavlyukovskiy:datasource-proxy-spring-boot-starter:2.0.0'
    implementation 'io.hypersistence:hypersistence-utils-hibernate-71:3.14.1'
    implementation 'org.apache.httpcomponents.client5:httpclient5'
    implementation 'org.flywaydb:flyway-database-postgresql'
    implementation "org.mapstruct:mapstruct:${mapstructVersion}"
    implementation "org.mapstruct.extensions.spring:mapstruct-spring-annotations:${mapstructSpringExtensionsVersion}"
    compileOnly 'org.projectlombok:lombok'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    runtimeOnly 'org.postgresql:postgresql'
    annotationProcessor "org.hibernate.orm:hibernate-processor"
    annotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"
    annotationProcessor "org.mapstruct.extensions.spring:mapstruct-spring-extensions:${mapstructSpringExtensionsVersion}"
    annotationProcessor "org.projectlombok:lombok-mapstruct-binding:${lombokMapstructBindingVersion}"
    annotationProcessor 'org.projectlombok:lombok'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    testImplementation 'org.springframework.boot:spring-boot-starter-actuator-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-data-jpa-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-flyway-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-restclient-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-validation-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-webmvc-test'
    testImplementation 'org.springframework.boot:spring-boot-testcontainers'
    testImplementation 'org.springframework.cloud:spring-cloud-starter-contract-stub-runner'
    testImplementation 'org.springframework.cloud:spring-cloud-starter-contract-verifier'
    testImplementation 'com.github.jsqlparser:jsqlparser:5.3'
    testImplementation 'io.rest-assured:rest-assured:6.0.0'
    testImplementation 'io.rest-assured:spring-mock-mvc:6.0.0'
    testImplementation 'net.datafaker:datafaker:2.5.3'
    testImplementation "org.mapstruct.extensions.spring:mapstruct-spring-test-extensions:${mapstructSpringExtensionsVersion}"
    testImplementation 'org.testcontainers:testcontainers-junit-jupiter'
    testImplementation 'org.testcontainers:testcontainers-postgresql'
    testImplementation 'org.wiremock.integrations:wiremock-spring-boot:4.0.8'
    testRuntimeOnly 'com.h2database:h2'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testAnnotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"
    testAnnotationProcessor "org.mapstruct.extensions.spring:mapstruct-spring-extensions:${mapstructSpringExtensionsVersion}"
    mockitoAgent("org.mockito:mockito-core") {
        transitive = false
    }
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

compileJava {
    options.compilerArgs += [
            '-Amapstruct.defaultComponentModel=spring',
            '-Amapstruct.defaultInjectionStrategy=constructor',
            '-Amapstruct.nullValueIterableMappingStrategy=RETURN_DEFAULT',
            '-Amapstruct.nullValueMapMappingStrategy=RETURN_DEFAULT',
            '-Amapstruct.suppressGeneratorTimestamp=true',
            '-Amapstruct.suppressGeneratorVersionInfoComment=true',
            '-Amapstruct.verbose=true'
    ]
}

contracts {
    packageWithBaseClasses = 'br.dev.allissonnunes.algashop.ordering.contract.base'
}

tasks.named('contractTest') {
    useJUnitPlatform()
}

tasks.named('test') {
    useJUnitPlatform()
}

test {
    filter {
        excludeTestsMatching('*IT')
    }
    jvmArgs += "-javaagent:${configurations.mockitoAgent.asPath}"
}

tasks.named('check') {
    dependsOn(test, contractTest, integrationTest)
}

tasks.register('integrationTest', Test) {
    description = 'Run integration tests.'
    group = 'verification'
    jvmArgs += "-javaagent:${configurations.mockitoAgent.asPath}"

    testClassesDirs = sourceSets.test.output.classesDirs
    classpath = sourceSets.test.runtimeClasspath

    shouldRunAfter(test)

    useJUnitPlatform()

    testLogging {
        events 'passed'
    }

    filter {
        includeTestsMatching('*IT')
        excludeTestsMatching('*Test')
    }
}

tasks.register('dockerBuild', Exec) {
    description = 'Builds a multi-platform Docker image using Buildx'
    group = 'build'
    dependsOn('bootJar')
    workingDir project.rootDir
    commandLine 'docker', 'buildx', 'build',
            '--build-arg', "JAR_NAME=${bootJar.archiveFileName.get()}",
            '--platform', 'linux/arm64/v8,linux/amd64',
            '--tag', "algashop/${project.name}:dev",
            '.'
}